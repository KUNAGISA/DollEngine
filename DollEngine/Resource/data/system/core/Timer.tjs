
class Timer
{
	var data = void; //Timer触发的方法需要的数据
	var handler = void; //Timer触发的方法
	var activeCount = 0; //从计时器启动开始执行过的次数
	var activeTime = 0; //从计时器启动开始经过的时间，单位ms
	var interval = 1000;//计时器触发间隔，单位ms
	var count = -1; //计时器执行次数
	function Timer (handler=void,data=void)
	{
		this.handler = handler;
		this.data = data;
	}

	function finalize () {
		enabled = false;
	}

	function onTimer () 
	{
		if (this.handler) {
			this.handler(this,this.data);
		}
		activeCount++;
		if (this.count>=0) {
			if (activeCount >= this.count) {
				invalidate this;
			}
		}
	}

	var _enabled = false;
	property enabled {
		setter(v) {
			if(_enabled != v){
				if(v){
					activeCount = 0;
					activeTime = 0;
					global.Timer.g_enabledTimers.add(this);
				}
				else {
					global.Timer.g_enabledTimers.remove(this);
				}
				_enabled = v;
			}
		}
		getter {
			return _enabled;
		}
	}
}

global.Timer.g_enabledTimers = new Set();